group 'com.github.apiggs'
version '1.5'

ext {
    DESCRIPTION = 'easy use apiggs with gradle.'
    SITE_URL = 'https://github.com/apiggs/apiggs-gradle-plugin'
    GIT_URL = 'https://github.com/apiggs/apiggs-gradle-plugin.git'

    LICENSE = 'MIT License'

    DEVELOPER_ID = 'ayz6uem'
    DEVELOPER_NAME = 'ayz6uem'
    DEVELOPER_EMAIL = '360188606@qq.com'

    IS_UPLOADING = project.getGradle().startParameter.taskNames.any{it.contains('bintrayUpload')}
}

buildscript {
    repositories {
        maven { url 'http://maven.aliyun.com/nexus/content/repositories/jcenter'}
    }
    dependencies {
        classpath 'com.novoda:bintray-release:0.8.1'
    }
}

apply plugin: 'java'
apply plugin: 'java-gradle-plugin'
apply plugin: 'com.novoda.bintray-release'
apply plugin: 'maven'

sourceCompatibility = 1.8

[compileJava,compileTestJava,javadoc]*.options*.encoding = 'UTF-8'

repositories {
    mavenLocal()
    maven { url 'http://maven.aliyun.com/nexus/content/repositories/jcenter'}
    mavenCentral()
}

dependencies {
    compile group:'com.github.apiggs', name:'apiggs', version: version

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

gradlePlugin {
    plugins {
        ApiggsPlugin {
            id = 'com.github.apiggs'
            implementationClass = 'com.github.apiggs.ApiggsPlugin'
        }
    }
}

publish {
    userOrg = "apiggs"
    groupId = group
    artifactId = name
    publishVersion = version
    desc = project.DESCRIPTION
    website = project.SITE_URL
//    bintrayUser = BINTRAYUSER
//    bintrayKey = BINTRAYKEY
    dryRun = false
}

gradle.taskGraph.whenReady { taskGraph ->
    taskGraph.getAllTasks().find {
        it.path == ":generatePomFileForMavenPublication"
    }.doLast {
        file("build/publications/maven/pom-default.xml").delete()
        println 'Overriding pom-file to make sure we can sync to maven central!'
        pom {
            //noinspection GroovyAssignabilityCheck
            project {
                name "$project.name"
                artifactId name
                packaging 'jar'
                description DESCRIPTION
                url SITE_URL
                version version

                scm {
                    url GIT_URL
                    connection GIT_URL
                    developerConnection GIT_URL
                }

                licenses {
                    license {
                        name LICENSE
                    }
                }

                developers {
                    developer {
                        id DEVELOPER_ID
                        name DEVELOPER_NAME
                        email DEVELOPER_EMAIL
                    }
                }
            }
        }.writeTo("build/publications/maven/pom-default.xml")
    }
}